plugins {
    id 'java'
    id 'maven-publish'
    id 'idea'
    id 'eclipse'
    id 'signing'
    id 'checkstyle'
    id 'jacoco'
    id "com.diffplug.spotless" version "6.14.1"
    id "org.sonarqube" version "4.0.0.2929"
    // Spotbugs gradle plugin version to spotbugs version https://github.com/spotbugs/spotbugs-gradle-plugin#spotbugs-version-mapping
    id "com.github.spotbugs" version "5.0.14"

    // Lombok plugin and gradle compat matrix https://github.com/freefair/gradle-plugins#compatibility-matrix
    id "io.freefair.lombok" version "6.6.3"
    id "io.github.gradle-nexus.publish-plugin" version "1.3.0"
}

ext {
    depCheckstyleVersion = "10.12.0"
    depCommonsVersion = "2.6"
    depHttpVersion = "4.5.14"
    depJacksonVersion = "2.9.8"
    depJacocoVersion = "0.8.3"
    depJunitVersion = "5.9.2"
    depLombokVersion = "1.18.16"
    depMockitoVersion = "5.2.0"
    depSlf4jVersion = "2.0.7"
}

apply from: 'gradle/quality.gradle'

description = "MapRoulette Java Client"
ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

sourceCompatibility=11
targetCompatibility=11

repositories {
    mavenCentral()
}

lombok {
    version = depLombokVersion
}

dependencies {
    implementation "commons-lang:commons-lang:${depCommonsVersion}"
    implementation "com.fasterxml.jackson.core:jackson-databind:${depJacksonVersion}"
    implementation "org.slf4j:slf4j-api:${depSlf4jVersion}"
    implementation "org.apache.httpcomponents:httpclient:${depHttpVersion}"

    testImplementation "org.slf4j:slf4j-simple:${depSlf4jVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:${depJunitVersion}"
    testImplementation "org.mockito:mockito-core:${depMockitoVersion}"

    integrationTestImplementation "org.slf4j:slf4j-simple:${depSlf4jVersion}"
    integrationTestImplementation "org.junit.jupiter:junit-jupiter-engine:${depJunitVersion}"

    checkstyle "com.puppycrawl.tools:checkstyle:${depCheckstyleVersion}"
}

task javadocJar(type: Jar) {
    archiveClassifier.set('javadoc')
    from javadoc
}

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

/*
 * This is to skip the tasks for which there is a skip<TaskName>=true
 * environment variable
 */
def skippedTaskNames = System.getenv().findAll { key, value ->
    key.startsWith("skip") && value.equalsIgnoreCase("true")
}.keySet().collect { it.substring(4) }
gradle.startParameter.excludedTaskNames += skippedTaskNames

idea {
    project {
        languageLevel = '11'
    }
}

signing {
    required { isReleaseVersion && gradle.taskGraph.hasTask("publish") }
    sign publishing.publications
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact(sourcesJar)
            artifact(javadocJar)

            pom {
                name = project_name
                description = project_description
                url = project_url

                scm {
                    connection = project_scm
                    developerConnection = project_scm
                    url = project_url
                }

                licenses {
                    license {
                        name = project_license_slug
                        url = project_license_url
                    }
                }

                developers {
                    developer {
                        id = project_developer
                        name = project_developer
                    }
                }
            }
        }
    }

    repositories {
        maven {
            name = "localPublish"
            url = layout.buildDirectory.dir('repos/localpublish')
        }
    }
}

// Developers use the 'publish' task to publish artifacts locally for inspection, and it's important
// not to interfere with that task. However, when the 'nexusPublishing' configuration is present,
// the 'publish' task will trigger the Sonatype publish tasks.
//
// We set up the 'nexusPublishing' block only for release versions or CI-driven snapshot publishes.
if (isReleaseVersion || Boolean.valueOf(project.property("publishSnapshot"))) {
    nexusPublishing {
        repositories {
            sonatype {
                username = System.getenv('SONATYPE_USERNAME')
                password = System.getenv('SONATYPE_PASSWORD')
            }
        }
    }
}
